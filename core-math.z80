            ; destroys: HL
negate:                                         ; num -- -num
            ld HL,0
            or A
            sbc HL,BC
            _setTOS  H,L
            ret

            ; destroys: HL
divide:                                         ; dividend divisor -- quotient remainder
            _pop HL  
            push DE
            ex DE,HL
            ld HL,0

            ld A,B
            call divid1
            ld B,A

            ld A,C
            ld C,B
            call divid1
            ld B,C
            ld C,A

            pop DE
            _push HL 
            ret

divid1:
            ld B,8
divid2:
            rla
            adc HL,HL
            sbc HL,DE
            jr nc,divid3
            add HL,DE
divid3:
            djnz divid2
            rla
            cpl
            ret

uMultiply:                                      ; u1 u2 -- plo phi
            _pop HL                             ; HL = u2, BC = u1
            push DE                             ; save DE .... suspend forth   
            ex DE,HL                            ; DE = u2                
            ld HL,0                             ; HLDE = 0 0 u2hi u2lo
            ld A,17                             ; loop counter
            or A                                ; clear carry
umul1:
            rr H
            rr L
            rr D
            rr E
            jr nc,umul2
            add HL,BC
umul2:
            dec A
            jr nz, umul1
            ld B,D
            ld C,E
            pop DE                              ; restore DE  resume forth
            _push HL
            ret

plus:                                           ; x y -- z
            push HL
            _pop HL
            add HL,BC
            _setTOS H,L
            pop HL
            ret

minus:                                           ; x y -- z
            push HL
            call swap
            _pop HL
            or a
            sbc HL,BC
            _setTOS H,L
            pop HL
            ret

carryToBool:
            ld BC,0
            ret nc
            dec BC
            ret

cells:
twoStar:
            SLA C
            RL	B            
            ret

            CODE "2/",0,twoSlash
            SRA B
            RR	C
            ret