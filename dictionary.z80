;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; dictionary routines
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

defword "find",0,FIND                   ; ptr len -- ptrOrZero
    _dup                                ; ptr len len
    ld BC, (VAR_LATEST)                 ; ptr len headptr
    _do
        checkZeroWord B,C               ; NULL pointer?  (end of the linked list)
        _until z
        _dup                            ; ptr len headptr headptr
        inc BC                          ; skip over backLink field
        inc BC
        ld A,(BC)                       ; A = flags
        inc BC                          ; point to packed string length field
        and FLAG_HIDDEN                 ; A = hidden flag value
        ld H,A                          ; save A
        ld A,(BC)                       ; A = length
        inc BC                          ; ptr len headptr nameptr
        add A,H                         ; A = length + hidden flag
        _over2                          ; ptr len headptr nameptr ptr len 
        cp C                            ; TOS = len. is length the same as A?
        _if z
            call compareStr             ; ptr len headptr result 
            checkZeroWord B,C           ; if TOS == 0 then matched
            _drop                       ; ptr len headptr
            _until z
        _else
            _drop3                      ; ptr len headptr 
        _endif
                                        ; ptr len headptr nameptr ptr len 
        loadWord H,L, B,C               ; HL = headptr
        readWord HL, B,C
    _enddo
                            ; ptr len headptr
    _dropNext               ; ptr headptr
    _dropNext               ; TOS = headptr
    ret

