;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; dictionary routines
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

defword "find",0,FIND                   ; ptr len -- ptrOrZero
    _push (latestDef)                   ; ptr len headptr
    _do
        _dup
    _untilZero                          ; NULL pointer?  (end of the linked list)
        _dup                            ; ptr len headptr headptr
        inc BC                          
        inc BC                          ; ptr len headptr flagsPtr 
        ld A,(BC)                       ; A = flags
        bit 7,A                         ; test HIDDEN flag
        _if nz
            _drop                       ; ptr len headptr
        _else
            inc BC                      ; ptr len headptr namePtr   ; length
            inc BC                      ; ptr len headptr namePtr'  ; first char
            _over2                      ; ptr len headptr namePtr' ptr len
            call compareStr             ; ptr len headptr match     ;match == 0
    _untilZero                          ; ptr len headptr 
        _endif
        _fetch                          ; ptr len headptr'
    _enddo
    _nip                                
    _nip                                ; headptr
    ret

; destroys: A
getBody:                                ; ptr -- ptr'
    inc BC                              ;
    inc BC                              ; skip over backlink
    inc BC                              ; skip over flags
    ld A,(BC)                           ; A = len of routine's name
    inc BC                              ; skip over len
    add A,C
    ld C,A
    ld A,0
    adc A,B
    ld B,A
    ret

