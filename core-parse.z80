parseWord:                              ; -- ptr len
    call dup                            ; dup delim
    call skipDelims
    call wordRead
    ret

skipDelims:                             ; delim -- char
    _do
        call getKey                     ; get next key, returned in A
        _switch

            bit 7, A                    ; check end of input, used by tests
            __ifnz skipDelims1 
                ld A,0
                _break
            __endif skipDelims1                  

            cp $5C                      ; \ start of a comment?
            _case z                     ; if yes skip to end of line
                _do
                    call getKey
                    cp "\r"             ; end of line yet?
                    __ifnz skipDelims2 
                        cp "\n"         ; end of line yet?
                    __endif skipDelims2
                _until z
                _enddo
            _endcase

            cp C                        ; = space?
            _case z
            _endcase

            cp " "                      ; < space?
            _case c
            _endcase

            ; default case
            _break

        _endswitch
    _enddo
    _setTOS 0,A                           ; TOS = char
    ret

; destroys: DE, HL
wordRead:                               ; ptr maxlen delim char -- ptr len
    ld A,C                              ;
    call drop                           ; ptr maxlen delim  ; A = char
    ld E,C                              ;
    call drop                           ; ptr maxlen        ; E = delim
    ld D,C                              ;
    call dropdup                        ; ptr ptr           ; D = maxlen
    or A                                ; A = 0 ?
    __ifz wordRead1
        ld BC,0                         ; ptr len           ; where len = 0
    __else wordRead1
        _pop HL                         ; ptr               ; HL = ptr
        _do
            ld (HL),A                   ; write char to lineBuf
            inc HL                      ; ptr ptr'
            call KEY                    ; get next key
            ld A,C
            call drop
            cp E                        ; = delim?
        _until z
            cp " "                      ; < space? i.e. control chars
        _until c
            dec D
        _until z
        _enddo
        or A
        sbc HL, BC                      ; ptr' - ptr
        _push  HL                       ; ptr len
    __endif wordRead1
    ret
