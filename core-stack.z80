; duplicate top of data stack
; x1 -- x1 x1
dup:
    ex DE,HL
    ld (HL), C                                ; PUSH register pair
    inc HL
    ld (HL), B
    inc HL
    ex DE,HL
    ret

; remove top of data stack
; x1 x2 -- x1
drop:
    ex DE,HL
    dec HL
    ld B,(HL)
    dec HL
    ld C,(HL)
    ex DE,HL
    ret

; duplicate NOS
; x1 x2 -- x1 x2 x1
over:
    ex DE,HL
    dec HL
    ld D,(HL)
    dec HL
    ld E,(HL)
    inc HL
    inc HL
    call dup
    ld B,D
    ld C,E
    ex DE,HL
    ret

; x1 x2 -- x2
nip:
    dec DE
    dec DE
    ret


; x1 x2 -- x2 x1
swap:
    ex DE,HL
    dec HL
    ld D,(HL)
    dec HL
    ld E,(HL)
    call dup
    ld B,D
    ld C,E
    ex DE,HL
    ret

tuck:                                ; x y -- y x y
    call swap
    call over
    ret

; ROT 	( x1 x2 x3 -- x2 x3 x1 )	
; Rotate the top three data stack items.

rot:
    _tor
    call swap
    _rfrom
    call swap
    ret

; 2SWAP	( x1 x2 x3 x4 -- x3 x4 x1 x2 )	
; Exchange the top two cell pairs.

swap2:
    call rot
    _tor
    call rot
    _rfrom
    ret

; 2DUP	( x1 x2 -- x1 x2 x1 x2 )	
; Duplicate cell pair x1 x2.

dup2:                                           ; xa xb -- xa xb xa xb
    call over
    call over
    ret

;   2DROP	( x1 x2 -- )			
;	Drop cell pair x1 x2 from the stack.

drop2:                                          ; x x --   ?????????????????
    call nip
    call drop
    ret

nip2:
    dec DE
    dec DE
    dec DE
    dec DE
    ret
    
;   2OVER	( x1 x2 x3 x4 -- x1 x2 x3 x4 x1 x2 )	  \ CORE
;	Copy cell pair x1 x2 to the top of the stack.
    
over2:
    _tor
    _tor
    call dup2
    _rfrom
    _rfrom
    call swap2
    ret

isZero:                                         ; n -- ?  ; this is wrong, should leave boolean
    _compareZero B,C
    call drop
    ret

; depth of data stack
; destroys: HL
CODE "depth", 0, depth                       ; -- n
    ld H,D
    ld L,E
    call dup
    ld BC,dataStack
    or A
    sbc HL,BC
    _setTOS  H,L
    _div2
    ret

