parseWord:                              ; -- ptr len
    _dup                                ; dup delim
    call skipDelims
    call wordRead
    ret

skipDelims:                             ; delim -- char
    _do
        call getKey                     ; get next key, returned in A
        _switch

            bit 7, A                    ; check end of input, used by tests
            _if nz
                ld A,0
                _break
            _endif

            cp $5C                      ; \ start of a comment?
            _case z                     ; if yes skip to end of line
                _do
                    call getKey
                    cp "\r"             ; end of line yet?
                    _if nz
                        cp "\n"         ; end of line yet?
                    _endif
                _until z
                _enddo
            _endcase

            cp C                        ; = space?
            _case z
            _endcase

            cp " "                      ; < space?
            _case c
            _endcase

            ; default case
            _break

        _endswitch
    _enddo
    _poke 0,A                           ; TOS = char
    ret

; destroys: A, DE, HL
wordRead:                               ; ptr maxlen delim char -- ptr len
    ld A,C                              ; 
    _drop                               ; ptr maxlen delim  ; A = char
    ld E,C                              ; 
    _drop                               ; ptr maxlen        ; E = delim
    ld D,C                              ; 
    _dropdup                            ; ptr ptr           ; D = maxlen
    or A                                ; A = 0 ?
    _if z
        ld BC,0                         ; ptr len           ; where len = 0
    _else
        _do
            ld (BC),A                   ; write char to lineBuf
            inc BC                      ; ptr ptr'
            call getKey                 ; get next key
            cp E                        ; = delim?
        _until z
            cp " "                      ; < space? i.e. control chars
        _until c
            dec D
        _until z
        _enddo
        _peek H,L                       ; HL = ptr'
        _dropdup                        ; ptr ptr
        or A
        sbc HL, BC                      ; ptr' - ptr
        _poke  H,L                      ; ptr len
    _endif
    ret
