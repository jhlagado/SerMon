FIND:
            _push (var_latest)                   ; ptr len headptr
find1:
            call dup
            call isZero
            jr z,find4                          ; NULL pointer?  (end of the linked list)
            call dup                            ; ptr len headptr headptr
            inc BC
            inc BC                              ; ptr len headptr flagsPtr
            ld A,(BC)                           ; A = flags
            bit 7,A                             ; test HIDDEN flag
            jr z, find2 
            call drop                           ; ptr len headptr
            jr find3
find2:
            inc BC                              ; ptr len headptr namePtr   ; length
            call dup                            ; ptr len headptr namePtr namePtr
            call cfetch                         ; ptr len headptr namePtr nlen
            call tor                                ; ptr len headptr namePtr r: nlen
            inc BC                              ; ptr len headptr namePtr'  ; first char
            call twoover                          ; ptr len headptr namePtr' ptr len
            call dup                            ; ptr len headptr namePtr' ptr len len
            call rfrom                              ; ptr len headptr namePtr' ptr len len nlen
            call minus                          ; ptr len headptr namePtr' ptr len diff
            call isZero                         ; ptr len headptr namePtr' ptr len
            jr z, find2A
            call drop                           ; ptr len headptr namePtr' ptr
            call drop                           ; ptr len headptr namePtr' 
            call drop                           ; ptr len headptr 
            jr find3
find2A:
            call compareStr                     ; ptr len headptr match     ;match == 0
            call isZero
            jr z,find4                          
find3:
            call fetch                          ; ptr len headptr'
            jr find1
find4:
            jp twonip                           ; headptr

getBody:                                        ; ptr -- ptr'
            inc BC                              ;
            inc BC                              ; skip over backlink
            inc BC                              ; skip over flags
            ld A,(BC)                           ; A = len of routine's name
            inc BC                              ; skip over len
            add A,C
            ld C,A
            ld A,0
            adc A,B
            ld B,A
            ret
