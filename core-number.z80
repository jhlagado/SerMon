CODE "formatNum", 0, formatNum       ; ptr num -- ptr len
    call over                           ; ptr num ptr
    call swap                           ; ptr ptr num
    _isNegative                         ; Detect sign of num
    __ifnz formatNum1 
        call negate                     ; ptr ptr num1
        call swap                       ; ptr num1 ptr ptr
        _pushChar '-'                   ; ptr num1 ptr '-'
        call over                       ; ptr num1 ptr '-' ptr
        call storeChar                  ; ptr num1 ptr
        inc BC                          ; ptr num1 ptr'
        call swap                       ; ptr ptr' num1
    __endif formatNum1
    _push (numberBase)                  ; ptr ptr' num1 base
    call swap                           ; ptr ptr' base num1
    ld D, 0                             ; D counter
    _do
        inc D
        call over                       ; ptr ptr' base num1 base
        call divide                     ; ptr ptr' base num2 rem
        _tor
        call dup                    ; ptr ptr' base num2 num2
    _untilZero
    _enddo
    call drop2                          ; ptr ptr'
    _do
        _rfrom
        call digitToChar                ; ptr ptr' ascii
        call over                       ; ptr ptr' ascii ptr'
        call storeChar                  ; ptr ptr'
        inc BC                          ; ptr ptr''
        dec D
    _until z
    _enddo
    call over                           ; ptr ptr'' ptr
    call minus                          ; ptr len
    ret

toNumber:                               ; str len -- num err
    call dup                            ; str len len
    _ifZero                             ; if zero then return 0 0
        call nip                        ; zero
        call dup                        ; zero zero
        ret                             ; -- 0 0
    _endif
    call swap
    _getTOS H,L
    call swap                           ; str len ; HL = str
    ld A,(HL)                           ; A = char
    cp '-'                              ; is A negative number?
    __ifz toNumber2
        inc HL
        dec BC                          ; str len
        call dup                        ; str len len
        _ifZero
            call nip                    ; zero
            call dup                    ; zero zero
            ld BC,1
            ret                         ; 0 1
        _else
            ld A,(HL)                   ; str len ; A = char
        _endIf
    __endIf toNumber2
    _push 0                             ; str len num(0)
    _do
        call dup
        ld C,A                          ; str len num char
        call charToDigit                ; str len num nib
        call dup                        ; str len num nib nib
        call isValidDigit               ; str len num nib bool
        _ifZero                         ; str len num nib
            call nip2                    ; str nib
            call drop                   ; str
            ld BC,0                     ; 0
            _push 1                     ; 0 1
            ret
        _endIf
        call plus                       ; str len num (num += A)
        call swap                       ; str num len
        dec BC                          ; str num len (len--)
        call dup                        ; str num len len
        call isZero                     ; str num len
        call swap                       ; str len num
    _until z
        _push (numberBase)              ; str len num base
        call uMultiply                  ; str len num num2 (num *= BASE)
        call drop                       ; str len num
        inc HL
        ld A,(HL)                       ; a = next char
    _enddo
    call nip                            ; str num
    call swap                           ; num str
    ld A,(BC)
    cp "-"
    call drop                           ; num
    __ifz toNumber3
        call negate                     ; num
    __endif toNumber3
    _push 0                             ; num 0
    ret

digitToChar:                            ; n -- a
    ld A,$30
    add A,C                             ; convert TOS digit to ASCII
    cp $3A
    __ifnc digitToChar1
        add A,7
    __endif digitToChar1
    ld C,A
    ret

charToDigit:                            ; char -- numOrError ; error = -1
    ld A,C
    ld B,0
    sub '0'                             ; < '0'?
    __ifnc charToDigit1                             ; invalid char, end of number
        cp 10                           ; <= '9'?
        __ifc charToDigit11
            ld C,A
            or A                        ; clear carry flag
        __else charToDigit11
            sub 17                      ; < 'A'? (17 is 'A'-'0')
            __ifnc charToDigit111
                add A,10
                ld C,A
            __endif charToDigit111
        __endif charToDigit11
    __endif charToDigit1
    __ifc charToDigit2
        ld BC,-1
    __endIf charToDigit2
    ret

; is in range 0 - BASE
isValidDigit:                           ; n -- b
    bit 7,B
    __ifnz isValidDigit1 
        ld BC,0
    __else isValidDigit1
        ld A,C
        ld BC, (numberBase)
        cp C
        call carryToBool
    __endIf isValidDigit1
    ret

