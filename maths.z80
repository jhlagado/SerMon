; destroys: HL
negate:                                 ; num -- -num
    ld HL,0
    or A
    sbc HL,BC
    _setTOS  H,L
    ret

; destroys: HL
divide:                                 ; dividend divisor -- quotient remainder
    push DE
    _getTOS D,E                           ; DE = divisor, BC = dividend
    call drop
    ld HL,0

    ld A,B
    call divide2
    ld B,A

    ld A,C
    ld C,B
    call divide2
    ld B,C
    ld C,A

    _push HL
    pop DE
    ret

divide2:
    ld B,8
    _do
        rla
        adc HL,HL
        sbc HL,DE
        _if c
            add HL,DE
        _endif
    _djnz
    rla
    cpl
    ret

; destroys: DE
uMultiply:                              ; u1 u2 -- plo phi
    push HL
    _getTOS D,E                           ; u1 u2
    call drop                               ; u1
    ld HL,0
    ld A,17                             ; loop counter
    or A                                ; clear carry
    _do
        rr H
        rr L
        rr D
        rr E
        _if c
            add HL,BC
        _endif
        dec A
    _until z
    _enddo
    _setTOS  D,E                          ; plo
    _push HL
    pop HL
    ret

plus:                                   ; x y -- z
    push HL
    _getTOS H,L
    call drop
    add HL,BC
    _setTOS H,L
    pop HL
    ret

minus:                                   ; x y -- z
    push HL
    _getTOS D,E
    call drop
    _getTOS H,L
    or A
    sbc HL,DE
    _setTOS H,L
    pop HL
    ret

carryToBool:
    ld BC,0
    _if c
        dec BC
    _endif
    ret

