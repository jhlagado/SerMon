;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; param stack
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; drop the top of param stack
.macro _drop                       ; x --
    _pop0 B,C		        ; drop top of stack
.endm

; duplicate the top of param stack
.macro _dup                        ; x -- x x
    _push0 B,C
.endm

; swap the top two items of param stack
.macro _swap                       ; x1 x2 -- x2 x1
    _pop0 D,E
    _push D,E
.endm

; reverse rot top three items, top of param stack becomes third from top  stack
.macro _nrot                       ; x1 x2 x3 -- x3 x1 x2
                            ; x3 in BC
    _pop0 D,E               ; x2 in DE
    _pop0 H,L               ; x1 in HL
    _push H,L               ; PUSH x1 - x3 x1
    _push D,E               ; PUSH x2 - x3 x1 x2
                            ; x2 in BC
.endm

; duplicate second from top of param stack
.macro _over                       ;x1 x2 -- x1 x2 x1
    _pop0 H,L
    _push0 H,L
    _push H,L
.endm

; rot top three items, third from top ends up on top of param stack
.macro _rot                        ; x1 x2 x3 -- x2 x3 x1
                            ; x3 in BC
    _pop0 D,E               ; x2 in DE
    _pop0 H,L               ; x1 in HL
    _push0 D,E              ; x2 on stack
    _push H,L               ; PUSH x1 - x2 x3 x1
                            ; x1 in BC
.endm


; Macros to deal with the param stack.
.macro _push0, regHI, regLO
    ld (IX+0), regLO     ; POP register pair from ret stk
    inc IX
    ld (IX+0), regHI
    inc IX
.endm

.macro _pop0, regHI, regLO
    dec IX              ; PUSH register pair on ret stack
    ld regHI, (IX+0)
    dec IX
    ld regLO, (IX+0)
.endm

; Macros to deal with the param stack with TOS in BC
.macro _push, regHI, regLO
    _push0 B,C             ; push TOS reg pair
    ld B, regHI         ; move register pair into TOS reg pair
    ld C, regLO
.endm

.macro _peek, regHI, regLO
    ld regHI, B         ; move TOS reg pair into register pair
    ld regLO, C
.endm

.macro _pop, regHI, regLO
    _peek regHI,regLO
    _pop0 B,C
.endm

.macro _pushValue, val
    _push0 B,C
    ld BC, val
.endm

.macro _pushText, s1, len
    _pushValue tpt_text%%M
    _pushValue len
    _skip
tpt_text%%M:
        db s1
    _endskip
.endm

.macro _pushPText, s1
    _pushValue tpt_text%%M
    ld BC, tpt_text%%M
    _skip
tpt_text%%M:
        .pstr s1
    _endskip
.endm

